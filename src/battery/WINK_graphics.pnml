/*
 *  Copyright (C) 2012-2019 Erato
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_battery_WINK_purchase, "gfx/purchase/battery/WINK.png") {
	template_purchase(0, 0)
}

/*
* VEHICLE
*/

//RNET
spriteset(spriteset_battery_WINK_front, "gfx/battery/WINK.png") {
	template_2cc_88(1,1)
}

spriteset(spriteset_battery_WINK_middle_pantograph, "gfx/battery/WINK.png") {
	template_2cc_38(1,32)
}

spriteset(spriteset_battery_WINK_middle, "gfx/battery/WINK.png") {
	template_2cc_38(1,95)
}

spriteset(spriteset_battery_WINK_back, "gfx/battery/WINK.png") {
	template_2cc_88(1,64)
}

switch(FEAT_TRAINS, SELF, switch_battery_WINK_middle_railtype, current_railtype) { 
	DC_B: spriteset_battery_WINK_middle_pantograph;
	spriteset_battery_WINK_middle;
}

switch(FEAT_TRAINS, SELF, switch_battery_WINK_position, (position_in_articulated_veh)) { 
	0: spriteset_battery_WINK_front;
	1: switch_battery_WINK_middle_railtype;
	spriteset_battery_WINK_back;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch (FEAT_TRAINS, SELF, switch_battery_WINK_articulated, extra_callback_info1) {
    1..2: return item_battery_WINK; //use same vehicle for all parts
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_battery_WINK_visual_effect_and_powered_railtype, current_railtype) {
    DC_B: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, switch_battery_WINK_visual_effect_and_powered, (position_in_articulated_veh)) {
    1: switch_battery_WINK_visual_effect_and_powered_railtype;
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/*
* Length
*/
switch(FEAT_TRAINS, SELF, switch_battery_WINK_length, (position_in_articulated_veh)) { 
	1: 3;
	8;
}

/*
* POWER CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_battery_WINK_power_railtype, current_railtype) {
    DC_B: return 1000;
    return 768;
}

switch(FEAT_TRAINS, SELF, switch_battery_WINK_power, param_track_power) {
    1: return switch_battery_WINK_power_railtype;
    return 1000;
}

switch(FEAT_TRAINS, SELF, switch_battery_WINK_purchasepower, param_track_power) {
    1: return 768;
    return 1000;
}
