/*
* PURCHASE
*/

spriteset(spriteset_steam_SS700_purchase, "gfx/purchase/steam/SS700.png") {
	template_purchase(0, 0)
}

spriteset(spriteset_steam_NS3700_purchase, "gfx/purchase/steam/NS3700.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_steam_SS700_livery_purchase, current_year) {
	0..1919: spriteset_steam_SS700_purchase;
    spriteset_steam_NS3700_purchase;
}

/*
* VEHICLE
*/

//6x engine animation
spriteset(spriteset_steam_SS700_ani_1, "gfx/steam/SS700.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_steam_SS700_ani_2, "gfx/steam/SS700.png") {
	template_2cc_68(1, 32)
}
spriteset(spriteset_steam_SS700_ani_3, "gfx/steam/SS700.png") {
	template_2cc_68(1, 64)
}
spriteset(spriteset_steam_SS700_ani_4, "gfx/steam/SS700.png") {
	template_2cc_68(1, 96)
}
spriteset(spriteset_steam_SS700_ani_5, "gfx/steam/SS700.png") {
	template_2cc_68(1, 128)
}
spriteset(spriteset_steam_SS700_ani_6, "gfx/steam/SS700.png") {
	template_2cc_68(1, 160)
}
//tender
spriteset(spriteset_steam_SS700_tender, "gfx/steam/SS700.png") {
	template_2cc_48(1, 192)
}

switch(FEAT_TRAINS, SELF, switch_steam_SS700_animation, (motion_counter)) {
    3..4:   spriteset_steam_SS700_ani_2;
    5..7:   spriteset_steam_SS700_ani_3;
    8..10:  spriteset_steam_SS700_ani_4;
    11..12: spriteset_steam_SS700_ani_5;
    13..15: spriteset_steam_SS700_ani_6;
    spriteset_steam_SS700_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_SS700_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_SS700_tender;
    switch_steam_SS700_animation;
}

//6x engine animation
spriteset(spriteset_steam_NS3700_ani_1, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 1)
}
spriteset(spriteset_steam_NS3700_ani_2, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 32)
}
spriteset(spriteset_steam_NS3700_ani_3, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 64)
}
spriteset(spriteset_steam_NS3700_ani_4, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 96)
}
spriteset(spriteset_steam_NS3700_ani_5, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 128)
}
spriteset(spriteset_steam_NS3700_ani_6, "gfx/steam/NS3700.png") {
	template_2cc_68(1, 160)
}
//tender
spriteset(spriteset_steam_NS3700_tender, "gfx/steam/NS3700.png") {
	template_2cc_48(1, 192)
}

switch(FEAT_TRAINS, SELF, switch_steam_NS3700_animation, (motion_counter)) {
    3..4:   spriteset_steam_NS3700_ani_2;
    5..7:   spriteset_steam_NS3700_ani_3;
    8..10:  spriteset_steam_NS3700_ani_4;
    11..12: spriteset_steam_NS3700_ani_5;
    13..15: spriteset_steam_NS3700_ani_6;
    spriteset_steam_NS3700_ani_1;
}

switch(FEAT_TRAINS, SELF, switch_steam_NS3700_position, (position_in_vehid_chain % 2)) {
	1: spriteset_steam_NS3700_tender;
    switch_steam_NS3700_animation;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_steam_SS700_autolivery, date_of_last_service) {
	0..date(1919,12,31): switch_steam_SS700_position;
    switch_steam_NS3700_position;
}

switch(FEAT_TRAINS, PARENT, switch_steam_SS700_purchaselivery, build_year) {
	0..1919: switch_steam_SS700_position;
    switch_steam_NS3700_position;
}

switch(FEAT_TRAINS, PARENT, switch_steam_SS700_livery, cargo_subtype) {
	1: switch_steam_SS700_purchaselivery;
    2: switch_steam_SS700_position;
    3: switch_steam_NS3700_position;
    switch_steam_SS700_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_steam_SS700_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_SS);
    3: return string(str_cargo_subtype_NS);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_steam_SS700_articulated, extra_callback_info1) {
    1: return item_steam_SS700;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VEHICLE LENGTH CALLBACK
*/


switch(FEAT_TRAINS, SELF, switch_steam_SS700_length, (position_in_vehid_chain % 2)) {
    1: return 4;
    return 6;
}
