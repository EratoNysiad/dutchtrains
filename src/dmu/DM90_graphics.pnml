/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_dmu_DM90_purchase, "gfx/purchase/dmu/DM90.png") {
	template_purchase(0, 0)
}

/*
* VEHICLE
*/

//ns
spriteset(spriteset_dmu_DM90_ns_front, "gfx/dmu/DM90.png") {
	template_2cc_88(0, 0)
}
spriteset(spriteset_dmu_DM90_ns_back, "gfx/dmu/DM90.png") {
	template_2cc_88(0, 30)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_position_ns, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DM90_ns_back;
    spriteset_dmu_DM90_ns_front;
}

//syntus
spriteset(spriteset_dmu_DM90_syntus_front, "gfx/dmu/DM90.png") {
	template_2cc_88(0, 60)
}
spriteset(spriteset_dmu_DM90_syntus_back, "gfx/dmu/DM90.png") {
	template_2cc_88(0, 90)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_position_syntus, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DM90_syntus_back;
    spriteset_dmu_DM90_syntus_front;
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_dmu_DM90_randomlivery_ns_syntus) {
	1: switch_dmu_DM90_position_ns;
    1: switch_dmu_DM90_position_syntus;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DM90_randomlivery, build_year) {
	0..1998: switch_dmu_DM90_position_ns;
    switch_dmu_DM90_randomlivery_ns_syntus;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DM90_livery, cargo_subtype) {
    1: switch_dmu_DM90_position_ns;
    2: switch_dmu_DM90_position_syntus;
    switch_dmu_DM90_randomlivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_dmu_DM90_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_random);
    1: return string(str_cargo_subtype_NS);
    2: return string(str_cargo_subtype_Syntus);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DM90_articulated, extra_callback_info1) {
    1: return item_dmu_DM90;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
