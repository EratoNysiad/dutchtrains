/*
* PURCHASE
*/

spriteset(spriteset_dmu_DE3_red_purchase, "gfx/purchase/dmu/DE3_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DE3_yellow_purchase, "gfx/purchase/dmu/DE3_yellow.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_livery_purchase, current_year) {
	0..1967: spriteset_dmu_DE3_red_purchase;
    spriteset_dmu_DE3_yellow_purchase;
}

/*
* VEHICLE
*/

//red
spriteset(spriteset_dmu_DE3_red_front, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 0)
}
spriteset(spriteset_dmu_DE3_red_middle, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 30)
}
spriteset(spriteset_dmu_DE3_red_back, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 60)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_position_red, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_DE3_red_middle;
    2: spriteset_dmu_DE3_red_back;
    spriteset_dmu_DE3_red_front;
}

//yellow
spriteset(spriteset_dmu_DE3_yellow_front, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 90)
}
spriteset(spriteset_dmu_DE3_yellow_middle, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 120)
}
spriteset(spriteset_dmu_DE3_yellow_back, "gfx/dmu/DE3.png") {
	template_2cc_88(0, 150)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_position_yellow, (position_in_vehid_chain % 3)) {
	1: spriteset_dmu_DE3_yellow_middle;
    2: spriteset_dmu_DE3_yellow_back;
    spriteset_dmu_DE3_yellow_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_autolivery, date_of_last_service) {
	0..date(1967,12,31): switch_dmu_DE3_position_red;
    switch_dmu_DE3_position_yellow;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_purchaselivery, build_year) {
	0..1967: switch_dmu_DE3_position_red;
    switch_dmu_DE3_position_yellow;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_livery, cargo_subtype) {
	1: switch_dmu_DE3_purchaselivery;
    2: switch_dmu_DE3_position_red;
    3: switch_dmu_DE3_position_yellow;
    switch_dmu_DE3_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_dmu_DE3_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_red);
    3: return string(str_cargo_subtype_yellow);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DE3_articulated, extra_callback_info1) {
    1..2: return item_dmu_DE3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}