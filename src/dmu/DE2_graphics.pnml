/*
* PURCHASE
*/
spriteset(spriteset_dmu_DE2_blue_purchase, "gfx/purchase/dmu/DE2_blue.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DE2_red_purchase, "gfx/purchase/dmu/DE2_red.png") {
	template_purchase(0, 0)
}
spriteset(spriteset_dmu_DE2_yellow_purchase, "gfx/purchase/dmu/DE2_yellow.png") {
	template_purchase(0, 0)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE2_livery_purchase, current_year) {
	0..1964: spriteset_dmu_DE2_blue_purchase;
    1965..1976: spriteset_dmu_DE2_red_purchase;
    spriteset_dmu_DE2_yellow_purchase;
}

/*
* VEHICLE
*/

//blue
spriteset(spriteset_dmu_DE2_blue_front, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 0)
}
spriteset(spriteset_dmu_DE2_blue_back, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 30)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE2_position_blue, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DE2_blue_back;
    spriteset_dmu_DE2_blue_front;
}

//red
spriteset(spriteset_dmu_DE2_red_front, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 60)
}
spriteset(spriteset_dmu_DE2_red_back, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 90)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE2_position_red, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DE2_red_back;
    spriteset_dmu_DE2_red_front;
}

//yellow
spriteset(spriteset_dmu_DE2_yellow_front, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 120)
}
spriteset(spriteset_dmu_DE2_yellow_back, "gfx/dmu/DE2.png") {
	template_2cc_88(0, 150)
}

switch(FEAT_TRAINS, SELF, switch_dmu_DE2_position_yellow, (position_in_vehid_chain % 2)) {
	1: spriteset_dmu_DE2_yellow_back;
    spriteset_dmu_DE2_yellow_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_dmu_DE2_autolivery, date_of_last_service) {
	0..date(1964,12,31): switch_dmu_DE2_position_blue;
    date(1965,1,1)..date(1976,12,31): switch_dmu_DE2_position_red;
    switch_dmu_DE2_position_yellow;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DE2_purchaselivery, build_year) {
	0..1964: switch_dmu_DE2_position_blue;
    1965..1976: switch_dmu_DE2_position_red;
    switch_dmu_DE2_position_yellow;
}

switch(FEAT_TRAINS, PARENT, switch_dmu_DE2_livery, cargo_subtype) {
	1: switch_dmu_DE2_purchaselivery;
    2: switch_dmu_DE2_position_blue;
    3: switch_dmu_DE2_position_red;
    4: switch_dmu_DE2_position_yellow;
    switch_dmu_DE2_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_dmu_DE2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_blue);
    3: return string(str_cargo_subtype_red);
    4: return string(str_cargo_subtype_yellow);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_dmu_DE2_articulated, extra_callback_info1) {
    1: return item_dmu_DE2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
