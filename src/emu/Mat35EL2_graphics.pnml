/*
* PURCHASE
*/

spriteset(spriteset_emu_Mat35EL2_purchase, "gfx/emu/Mat35EL2.png") {
	template_purchase(1, 128)
}

/*
* VEHICLE
*/

//silver roof
spriteset(spriteset_emu_Mat35EL2_silverroof_front, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_emu_Mat35EL2_silverroof_back, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 32)
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat35EL2_position_silverroof, (position_in_vehid_chain % 2)) {
	1: spriteset_emu_Mat35EL2_silverroof_back;
    spriteset_emu_Mat35EL2_silverroof_front;
}

//grey roof
spriteset(spriteset_emu_Mat35EL2_greyroof_front, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_emu_Mat35EL2_greyroof_back, "gfx/emu/Mat35EL2.png") {
	template_2cc_88(1, 96)
}

switch(FEAT_TRAINS, SELF, switch_emu_Mat35EL2_position_greyroof, (position_in_vehid_chain % 2)) {
	1: spriteset_emu_Mat35EL2_greyroof_back;
    spriteset_emu_Mat35EL2_greyroof_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35EL2_autolivery, date_of_last_service) {
	0..date(1938,12,31): switch_emu_Mat35EL2_position_silverroof;
    switch_emu_Mat35EL2_position_greyroof;
}

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35EL2_purchaselivery, build_year) {
	0..1938: switch_emu_Mat35EL2_position_silverroof;
    switch_emu_Mat35EL2_position_greyroof;
}

switch(FEAT_TRAINS, PARENT, switch_emu_Mat35EL2_livery, cargo_subtype) {
	1: switch_emu_Mat35EL2_purchaselivery;
    2: switch_emu_Mat35EL2_position_silverroof;
    3: switch_emu_Mat35EL2_position_greyroof;
    switch_emu_Mat35EL2_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_emu_Mat35EL2_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_olive_silverroof);
    3: return string(str_cargo_subtype_olive_greyroof);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_Mat35EL2_articulated, extra_callback_info1) {
    1: return item_emu_Mat35EL2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
