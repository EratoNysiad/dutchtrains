/*
 *  Copyright (C) 2012 Jasper Vries
 *  This file is part of The Dutch Trainset 2
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

/*
* PURCHASE
*/

spriteset(spriteset_emu_ICM_purchase, "gfx/purchase/emu/ICM.png") {
	template_purchase(0, 0)
}

/*
* VEHICLE
*/

//original
spriteset(spriteset_emu_ICM_original_front, "gfx/emu/ICM.png") {
	template_2cc_88(0, 0)
}
spriteset(spriteset_emu_ICM_original_middle, "gfx/emu/ICM.png") {
	template_2cc_88(0, 30)
}
spriteset(spriteset_emu_ICM_original_back, "gfx/emu/ICM.png") {
	template_2cc_88(0, 60)
}
spriteset(spriteset_emu_ICM_original_front_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 90)
}
spriteset(spriteset_emu_ICM_original_back_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 120)
}


//groninger museum
spriteset(spriteset_emu_ICM_whitedoors_front, "gfx/emu/ICM.png") {
	template_2cc_88(0, 150)
}
spriteset(spriteset_emu_ICM_whitedoors_middle, "gfx/emu/ICM.png") {
	template_2cc_88(0, 180)
}
spriteset(spriteset_emu_ICM_whitedoors_back, "gfx/emu/ICM.png") {
	template_2cc_88(0, 210)
}
spriteset(spriteset_emu_ICM_whitedoors_front_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 240)
}
spriteset(spriteset_emu_ICM_whitedoors_back_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 270)
}

//olympic
spriteset(spriteset_emu_ICM_olympic_front, "gfx/emu/ICM.png") {
	template_2cc_88(0, 300)
}
spriteset(spriteset_emu_ICM_olympic_middle, "gfx/emu/ICM.png") {
	template_2cc_88(0, 330)
}
spriteset(spriteset_emu_ICM_olympic_back, "gfx/emu/ICM.png") {
	template_2cc_88(0, 360)
}
spriteset(spriteset_emu_ICM_olympic_front_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 390)
}
spriteset(spriteset_emu_ICM_olympic_back_open, "gfx/emu/ICM.png") {
	template_2cc_88(0, 420)
}

//renewal
spriteset(spriteset_emu_ICM_renewal_front, "gfx/emu/ICM.png") {
	template_2cc_88(0, 450)
}
spriteset(spriteset_emu_ICM_renewal_middle, "gfx/emu/ICM.png") {
	template_2cc_88(0, 480)
}
spriteset(spriteset_emu_ICM_renewal_back, "gfx/emu/ICM.png") {
	template_2cc_88(0, 510)
}


//original
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_original_position_front, position_in_consist) {
	0: spriteset_emu_ICM_original_front;
    spriteset_emu_ICM_original_front_open;
}
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_original_position_back, position_in_consist_from_end) {
	0: spriteset_emu_ICM_original_back;
    spriteset_emu_ICM_original_back_open;
}
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_original_position, (position_in_vehid_chain % 4)) {
	1..2: spriteset_emu_ICM_original_middle;
    3: switch_emu_ICM4_original_position_back;
    switch_emu_ICM4_original_position_front;
}

//olympic
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_olympic_position_front, position_in_consist) {
	0: spriteset_emu_ICM_olympic_front;
    spriteset_emu_ICM_olympic_front_open;
}
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_olympic_position_back, position_in_consist_from_end) {
	0: spriteset_emu_ICM_olympic_back;
    spriteset_emu_ICM_olympic_back_open;
}
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_olympic_position, (position_in_vehid_chain % 4)) {
	1..2: spriteset_emu_ICM_olympic_middle;
    3: switch_emu_ICM4_olympic_position_back;
    switch_emu_ICM4_olympic_position_front;
}

//renewal
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_renewal_position, (position_in_vehid_chain % 4)) {
	1..2: spriteset_emu_ICM_renewal_middle;
    3: spriteset_emu_ICM_renewal_back;
    spriteset_emu_ICM_renewal_front;
}

/*
* LIVERY SELECTION
*/

random_switch(FEAT_TRAINS, PARENT, switch_emu_ICM4_autolivery_random_olympic_original) {
	47: switch_emu_ICM4_original_position;
    3: switch_emu_ICM4_olympic_position;
}

random_switch(FEAT_TRAINS, PARENT, switch_emu_ICM4_autolivery_random_olympic_renewal) {
	47: switch_emu_ICM4_renewal_position;
    3: switch_emu_ICM4_olympic_position;
}

switch(FEAT_TRAINS, PARENT, switch_emu_ICM4_autolivery, date_of_last_service) {
	0..date(2003,12,31): switch_emu_ICM4_original_position;
	date(2004,1,1)..date(2006,12,31): switch_emu_ICM4_autolivery_random_olympic_original;
	date(2007,1,1)..date(2011,12,31): switch_emu_ICM4_autolivery_random_olympic_renewal;
    switch_emu_ICM4_renewal_position;
}

switch(FEAT_TRAINS, PARENT, switch_emu_ICM4_purchaselivery, build_year) {
	0..2003: switch_emu_ICM4_original_position;
	2004..2006: switch_emu_ICM4_autolivery_random_olympic_original;
	2007..2011: switch_emu_ICM4_autolivery_random_olympic_renewal;
    switch_emu_ICM4_renewal_position;
}

switch(FEAT_TRAINS, PARENT, switch_emu_ICM4_livery, cargo_subtype) {
	1: switch_emu_ICM4_purchaselivery;
    2: switch_emu_ICM4_original_position;
    3: switch_emu_ICM4_renewal_position;
    4: switch_emu_ICM4_olympic_position;
    switch_emu_ICM4_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, SELF, switch_emu_ICM4_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_original);
    3: return string(str_cargo_subtype_renewal);
    4: return string(str_cargo_subtype_olympic);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_ICM4_articulated, extra_callback_info1) {
    1..3: return item_emu_ICM4;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/*
* VISUAL EFFECT AND POWERED CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_emu_ICM4_visual_effect_and_powered, (position_in_vehid_chain % 4)) {
    0: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, DISABLE_WAGON_POWER);
    return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}
