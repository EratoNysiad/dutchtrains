/*
* PURCHASE
*/

spriteset(spriteset_metro_RET_typeT_blue_purchase, "gfx/metro/RET_typeT.png") {
	template_purchase(1, 192)
}

/*
* VEHICLE
*/

spriteset(spriteset_metro_RET_typeT_green_front, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 1)
}
spriteset(spriteset_metro_RET_typeT_green_back, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 32)
}

spriteset(spriteset_metro_RET_typeT_red_front, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 64)
}
spriteset(spriteset_metro_RET_typeT_red_back, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 96)
}

spriteset(spriteset_metro_RET_typeT_blue_front, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 128)
}
spriteset(spriteset_metro_RET_typeT_blue_back, "gfx/metro/RET_typeT.png") {
	template_2cc_88(1, 160)
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeT_position_green, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_RET_typeT_green_back;
    spriteset_metro_RET_typeT_green_front;
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeT_position_red, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_RET_typeT_red_back;
    spriteset_metro_RET_typeT_red_front;
}

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeT_position_blue, (position_in_vehid_chain % 2)) {
	1: spriteset_metro_RET_typeT_blue_back;
    spriteset_metro_RET_typeT_blue_front;
}

/*
* LIVERY SELECTION
*/

switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeT_autolivery, date_of_last_service) {
	0..date(1997,12,31): switch_metro_RET_typeT_position_red;
    date(2006,1,1)..date(2009,12,31): switch_metro_RET_typeT_position_blue;
    switch_metro_RET_typeT_position_green;
}

switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeT_purchaselivery, build_year) {
	0..1997: switch_metro_RET_typeT_position_red;
    2006..2009: switch_metro_RET_typeT_position_blue;
    switch_metro_RET_typeT_position_green;
}

switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeT_livery, cargo_subtype) {
	1: switch_metro_RET_typeT_purchaselivery;
    2: switch_metro_RET_typeT_position_red;
    3: switch_metro_RET_typeT_position_green;
    4: switch_metro_RET_typeT_position_blue;
    switch_metro_RET_typeT_autolivery;
}

// CARGO SUBTYPE CALLBACK
switch(FEAT_TRAINS, PARENT, switch_metro_RET_typeT_subtype_text, cargo_subtype) {
    0: return string(str_cargo_subtype_auto);
	1: return string(str_cargo_subtype_purchase);
    2: return string(str_cargo_subtype_red_blue);
    3: return string(str_cargo_subtype_silver_green);
    4: return string(str_cargo_subtype_randstadrail);
    return CB_RESULT_NO_TEXT;
}

/*
* ARTICULATED VEHICLE CALLBACK
*/

switch(FEAT_TRAINS, SELF, switch_metro_RET_typeT_articulated, extra_callback_info1) {
    1: return item_metro_RET_typeT;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
